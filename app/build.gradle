plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'org.jlleitschuh.gradle.ktlint'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'com.google.firebase.firebase-perf'
    id 'com.google.devtools.ksp'
}

def versionMajor = 1
def versionMinor = 2
def versionPatch = 0
def versionBuild = 0  // bump for dogfood builds, public betas, etc.


android {
    compileSdkVersion = 34
    buildToolsVersion = "34.0.0"

    defaultConfig {
        applicationId "in.technowolf.ipscanner"
        minSdkVersion 26
        targetSdkVersion 34
        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}" + (versionPatch == 0 ? "" : ".${versionPatch}")

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.debug
    }

    Properties properties = new Properties()
    def localPropertiesFile = project.rootProject.file('local.properties')
    if (localPropertiesFile.exists()) {
        properties.load(localPropertiesFile.newInputStream())
    }

    buildFeatures {
        buildConfig = true
        viewBinding true
    }

    signingConfigs {
        dev {
            storeFile file('dev-key.jks')
            storePassword System.getenv("keystorePassword") ?: properties.getProperty("keystorePassword")
            keyAlias 'ipscanner'
            keyPassword System.getenv("keystorePassword") ?: properties.getProperty("keystorePassword")
        }
        production {
            storeFile file('prod-key.jks')
            storePassword System.getenv("keystorePassword") ?: properties.getProperty("keystorePasswordProd")
            keyAlias 'ip-scanner'
            keyPassword System.getenv("keystorePassword") ?: properties.getProperty("keystorePasswordProd")
        }
    }

    flavorDimensions = ["IpScanner"]
    productFlavors {
        dev {
            dimension "IpScanner"
            versionNameSuffix '-dev'
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.dev
        }
        production {
            dimension "IpScanner"
            signingConfig signingConfigs.production
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    ktlint {
        debug = true
        verbose = true
        android = true
        outputToConsole = true
        outputColorName = "RED"
        ignoreFailures = false
        disabledRules = ["import-ordering", "indent","multiline-if-else","argument-list-wrapping"]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    namespace 'in.technowolf.ipscanner'
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.7'

    // Android Preferences
    implementation 'androidx.preference:preference-ktx:1.2.1'

    // Testing Libraries
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation('com.adevinta.android:barista:4.3.0') {
        exclude group: 'org.jetbrains.kotlin'
    }

    // Jetpack Emoji library, Downloadable and bundled both.
    implementation 'androidx.emoji:emoji:1.1.0'
    implementation 'androidx.emoji:emoji-bundled:1.1.0'

    // Koin main features for Android (Scope,ViewModel ...)
    implementation 'io.insert-koin:koin-android:3.5.3'

    // Retrofit for network calls
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Okhttp as http client in retrofit
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'

    // Moshi to serialize and deserialize json response.
    implementation 'com.squareup.moshi:moshi-kotlin:1.15.1'
    ksp 'com.squareup.moshi:moshi-kotlin-codegen:1.15.1'

    // Converter for Moshi to work with retrofit
    implementation 'com.squareup.retrofit2:converter-moshi:2.9.0'

    // Chucker to intercept network calls, Works with okhttp client.
    debugImplementation 'com.github.chuckerteam.chucker:library:4.0.0'
    releaseImplementation 'com.github.chuckerteam.chucker:library-no-op:4.0.0'

    // Lifecycle components
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.7.0'

    // Ip validation view
    implementation 'com.gitlab.technowolf:ip-validation-view:1.1.5'

    // Room DB
    implementation 'androidx.room:room-runtime:2.6.1'
    ksp 'androidx.room:room-compiler:2.6.1'
    implementation 'androidx.room:room-ktx:2.6.1'

    // Kotlin Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.0'

    // LeakCanary to detect memory leaks
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.13'

    // Firebase dependencies
    implementation 'com.google.firebase:firebase-crashlytics-ktx:18.6.2'
    implementation 'com.google.firebase:firebase-analytics-ktx:21.5.1'
    implementation 'com.google.firebase:firebase-perf-ktx:20.5.2'

}